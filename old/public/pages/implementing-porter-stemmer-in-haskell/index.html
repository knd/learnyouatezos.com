<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en-us" lang="en-us">
<head>
  <link href="http://gmpg.org/xfn/11" rel="profile">
  <meta http-equiv="content-type" content="text/html; charset=utf-8">
  <meta name="generator" content="Hugo 0.18.1" />

  
  <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1">

  
  <title>Implementing Porter Stemmer in Haskell &middot; Latte, Jed?</title>
  

  
  <link rel="stylesheet" href="http://lattejed.com/css/poole.css">
  <link rel="stylesheet" href="http://lattejed.com/css/syntax.css">
  <link rel="stylesheet" href="http://lattejed.com/css/hyde.css">
  <link rel="stylesheet" href="https://fonts.googleapis.com/css?family=PT+Sans:400,400italic,700|Abril+Fatface">

  
  <link rel="shortcut icon" href="/blkstar.co.logo_128.bt.png">

  
  <link href="" rel="alternate" type="application/rss+xml" title="Latte, Jed?" />

  <link rel="stylesheet" href="http://lattejed.com/css/highlight/default.css">
  <script src="http://lattejed.com/js/highlight.pack.js"></script>
  <script>hljs.initHighlightingOnLoad();</script>
</head>

<body class="theme-base-08 ">

    <div class="content container">
        <div class="post">
            <h1>Implementing Porter Stemmer in Haskell</h1>
            <span class="post-date">Mon, Jun 10, 2013</span>
            

<p>I recently started learning Haskell. Like many programmers who get interested in the language I&rsquo;ve spent as much time studying the language as I have trying to find an excuse to actually use it. It&rsquo;s a somewhat difficult language to learn &ndash; at least if you haven&rsquo;t worked with functional programming before &ndash; and it&rsquo;s not really a go-to language for most situations. Finding a reason to use it can be challenging.</p>

<p>My answer to this has been to work on projects that don&rsquo;t have an immediate application. For reasons I won&rsquo;t bore you with, I decided to (re)implement Porter&rsquo;s stemming algorithm. The point of a stemming algorithm is to reduce words to common roots, which is often used as a pre-processing step when building a search engine or doing other NLP work. For example, the words &ldquo;stemmer&rdquo;, &ldquo;stemming&rdquo;, &ldquo;stemmed&rdquo; are all based on &ldquo;stem&rdquo; and share a common meaning. Reducing these words to their root, &ldquo;stem&rdquo;, makes processing them further a lot easier.</p>

<p>Porter&rsquo;s English word stemming algorithm is described <a href="http://tartarus.org/martin/PorterStemmer/def.txt">here</a> and there&rsquo;s a Python implementation <a href="https://hkn.eecs.berkeley.edu/~dyoo/python/py_lovins/porter.py">here</a> and a Haskell implementation <a href="http://tartarus.org/martin/PorterStemmer/haskell.txt">here</a>. I&rsquo;m so new to Haskell (and functional programming) that I wanted access to both the original paper, a version in a language I&rsquo;m familiar with as well as something in Haskell. Having said that, after I worked on this algorithm for a while it became apparent that working from the Python version was actually a hindrance compared to just working from the paper. Since our functional approach will be so different from our imperative approach, one doesn&rsquo;t really inform the other.</p>

<h3 id="a-tiny-bit-about-haskell">A Tiny Bit About Haskell</h3>

<p>Haskell is a purely functional language. Purely functional means that the functions are pure in a mathematical sense. They&rsquo;re free (with some exceptions) from side effects. If you give a Haskell function a given input, it will always return the same output. Functions are also first class and the main control structure used in the language. It&rsquo;s not a stretch to say that everything is a function in Haskell. Haskell is lazy. An infinite list in Haskell is valid. That&rsquo;s made possible by lazy evaluation, i.e., the nth value of a list isn&rsquo;t &ldquo;created&rdquo; until it&rsquo;s needed.</p>

<p>Haskell lacks a lot of the stuff you&rsquo;re familiar with if you&rsquo;re coming from an imperative background. For control flow there are conditionals but no loops. If you need to do something repeatedly you&rsquo;ll generally use a recursive function or operate on a list. Haskell doesn&rsquo;t have mutable variables (well it does, but not by default) &ndash; with some exceptions, it doesn&rsquo;t have state. Again, to do most things in Haskell you do so without any mutable state. No local variables inside functions to hold intermediate values (or examine them). No variables further up in scope to hold values between function calls. It&rsquo;s a mad, mad world.</p>

<p>So why Haskell or functional programming in general? Well Haskell&rsquo;s purity makes it much easier to write bug-free code and write tests for that code. Functions are black boxes that can be tested without elaborate setup or teardown. They can be tested in isolation and will operate the same when working with the rest of the program. Having no side effects has its advantages. It&rsquo;s also a terse language. While it&rsquo;s debatable that writing less code (to do the same thing) makes anyone more productive, it is, at least for me, better to write and read terse languages.</p>

<p>I&rsquo;ve only scratched the surface here. For a more eloquent and informative overview of the benefits this is a good place to start. While we&rsquo;re talking about learning materials, I can&rsquo;t recommend <a href="http://learnyouahaskell.com/">Learn You a Haskell for Great Good</a> enough. It&rsquo;s humorous and dare I say beautifully illustrated &ndash; at least if you like the doodles of people who are obviously too smart for their own good.</p>

<h3 id="let-there-be-stems">Let There Be Stems</h3>

<p>Enough bs. Let&rsquo;s write some Haskell. The Porter Stemmer algorithm kind of breaks down like this: We make some definitions (such as what&rsquo;s a consonant or a vowel, how many consonant sequences a word has) and then we apply a series of rules to our word to get its stem. I started with our definition of a consonant and vowel. From the paper, we define them as such:</p>

<blockquote>
<p>A \consonant\ in a word is a letter other than A, E, I, O or U, and other than Y preceded by a consonant.
From our Python implementation, this definition is handled like this:</p>
</blockquote>

<pre><code class="language-python">def cons(self, i):
    &quot;&quot;&quot;cons(i) is TRUE &lt;=&gt; b[i] is a consonant.&quot;&quot;&quot;
    if self.b[i] == 'a' or self.b[i] == 'e' or self.b[i] == 'i' or self.b[i] == 'o' or self.b[i] == 'u':
        return 0
    if self.b[i] == 'y':
        if i == self.k0:
            return 1
        else:
            return (not self.cons(i - 1))
    return 1
</code></pre>

<p>This is pretty straightforward. Our function cons returns True (or 1 in this case) if b (our word as a string in an instance variable) at the index in question is in the set &ldquo;aeiou&rdquo; or is both &lsquo;y&rsquo; and the preceding letter is not in the set &ldquo;aeiou&rdquo;. This function isn&rsquo;t particularly elegant but it&rsquo;s easy to understand and it gets the job done. Vowels then are anything where cons returns False.</p>

<p>There are a couple of problems here for the beginning Haskeller. We don&rsquo;t have our instance variable b for one. The other isn&rsquo;t a problem as much as it is a style or philosophy issue. I think you could argue that accessing lists by index isn&rsquo;t very Haskelly. We have an operator for it, !!, but using it means bounds checking and passing our index along with our string to the function. While this is certainly possible (the Haskell implementation listed here does it exactly that way) I wanted to find a solution that felt more natural in the context.</p>

<p>My only functional background comes from using the functional features present in Python. Specifically I&rsquo;m comfortable with list comprehension as well as &ldquo;decorating&rdquo; values (in a way similar to the decorate-sort-undecorate pattern in Python) in tuples and &ldquo;zipping&rdquo; lists. In this case we want to not only figure out if the characters in a word are vowel or consonants, we&rsquo;re going to want to look at the patterns of vowels and consonants that exist in a word. This will be useful later in the algorithm. Given this, it seemed natural to figure out the entire word at once and keep that pattern as a string in a tuple along with our original word.</p>

<p>In Haskell I came up with this:</p>

<pre><code class="language-haskell">-- Make a pattern to match the consonants and vowels in a word where a cons is 
-- not &quot;aeiou&quot; or y preceded by a cons e.g., &quot;happy&quot; -&gt; (&quot;happy&quot;, &quot;cvccv&quot;)
wordDesc :: String -&gt; (String, String)
wordDesc str = (str, [corv (a,b,i) | (a,b,i) &lt;- zip3 str (rotateR str) [0..len]])
    where len = length str - 1
          corv (a,b,i)
            | a == 'y' &amp;&amp; i /= 0 &amp;&amp; b `notElem` vs = 'v'            
            | a `elem` vs = 'v'
            | otherwise = 'c'
            where vs = &quot;aeiou&quot;
</code></pre>

<p>The first interesting bit here is <code>wordDesc :: String -&gt; (String, String)</code>. This is our function&rsquo;s type declaration. These are optional but considered good practice. If we don&rsquo;t explicitly define it, the compiler/interpreter will figure it out for itself. In this case, it says that we&rsquo;re going to take a String and return a tuple <code>(String, String)</code>. Our first string will be our original word, unchanged, and our second will be the matching pattern of consonant or vowel definitions. If we called it with &ldquo;tree&rdquo;, it would give us (&ldquo;tree&rdquo;, &ldquo;ccvv&rdquo;), since our consonants are &ldquo;tr&rdquo; and our vowels &ldquo;ee&rdquo;.</p>

<p>The meat of this function is the list comprehension <code>[corv (a,b,i) | (a,b,i) &lt;- zip3 str (rotateR str) [0..len]]</code>. This might be a bit confusing if it&rsquo;s new to you. It&rsquo;s of the form <code>[val | val &lt;- list]</code>. This is very similar to <code>[val for val in list]</code> in Python. Our list in this case is actually three lists zipped together, <code>zip3 str (rotateR str) [0..len]</code>: Our original list (our string), that list rotated one letter to the right and the list 0 to the length of our word (less one since we&rsquo;re zero indexed).</p>

<p>The first list is obvious, but what are the other two lists for? In the case of a &lsquo;y&rsquo; we have to look at two things: the preceding letter (whether or not it&rsquo;s a consonant) and the index of our &lsquo;y&rsquo;. If our &lsquo;y&rsquo; isn&rsquo;t at the start of a word and has a consonant before it, then it&rsquo;s a vowel for our purposes. Since I didn&rsquo;t want to pass an index into this function I decided to look at the current letter and the previous letter simultaneously. Rotating the list one letter to the right &ldquo;lines up&rdquo; our current and previous letters and in this case makes them available in the variables a and b. We still ended up needing that index to check if we&rsquo;re looking at our first letter or not. Not wanting to belabor the &ldquo;no index&rdquo; edict any further I zipped in a list of index values for that purpose. There is likely another, non-indexed, way to do this but I decided to make a concession and move on. Since we only have index values that match our actual string, we don&rsquo;t have to worry about bounds checking at least.</p>

<p>The pipes in our function definition are called guards. They&rsquo;re one way Haskell allows us to define conditional statements. The guards are evaluated in order and the provided value is assigned for the first condition that evaluates as true. The otherwise statement is mandatory and defines our default value. There where constructions allow us to define parts of our main function separately to improve readability. In this case we&rsquo;re defining len (the length of our string, zero-indexed) and corv, which returns a &lsquo;c&rsquo; or a &lsquo;v&rsquo; based on the criteria that we&rsquo;ve defined.</p>

<p>Our rotateR function is simply:</p>

<pre><code class="language-haskell">rotateR :: [a] -&gt; [a]
rotateR [x] = [x]
rotateR xs = last xs : init xs
</code></pre>

<p>There are some other interesting Haskell bits here. First our type declaration is the variable a instead of a defined type. These are called &ldquo;type variables&rdquo; in Haskell. Our declaration here states that we&rsquo;ll take a list of type a and return a list of that same type. For our purposes, we could have used the declaration <code>rotateR :: String -&gt; String</code>. Using type variables makes the function more generic.</p>

<p>The other really interesting thing going on here is called pattern matching. Pattern matching in Haskell allows us to create a set of functions that share a type declaration (and a name) but can return different results based on different attributes of the arguments they&rsquo;re called with. If we call our <code>rotateR</code> with a list of length one (defined as <code>[x]</code> here) we&rsquo;ll just return the list unchanged, since a list of length one, rotated, is itself. If we call it with a list of length &gt; 1, we&rsquo;ll actually rotate it. The variable xs in this case means any list. Since the pattern matching is attempted in descending order, our first function will catch all lists of length one and our second function will catch all other lists. This is powerful stuff and makes it very easy to implement recursive functions and deal with edge cases.</p>

<p>To make this function more robust, we could could add in <code>rotateR []</code> as a pattern so we could define how the function handles being passed an empty list. This is a common pattern in Haskell.</p>

<p>This algorithm is broken down into 5 steps which process a word in order. I&rsquo;ll describe how I tackled step one in a moment. Before I do, I&rsquo;ll go over some of the foundational parts of the algorithm that we need in place before we go through the steps.</p>

<pre><code class="language-haskell">rotateR :: [a] -&gt; [a]
rotateR [x] = [x]
rotateR xs = last xs : init xs

-- Make a pattern to match the consonants and vowels in a word where a cons is
-- not &quot;aeiou&quot; or y preceded by a cons e.g., &quot;happy&quot; -&gt; (&quot;happy&quot;, &quot;cvccv&quot;)
wordDesc :: String -&gt; (String, String)
wordDesc str =
    (str, [corv (a,b,i) | (a,b,i) &lt;- zip3 str (rotateR str) [0..len]])
    where len = length str - 1
          corv (a,b,i)
            | a == 'y' &amp;&amp; i /= 0 &amp;&amp; b `notElem` vs = 'v'
            | a `elem` vs = 'v'
            | otherwise = 'c'
            where vs = &quot;aeiou&quot;

-- Measure the number of consonant sequences in the word in the form
-- [c]vcvc[v] == 2 where the inner 'vc' sequences are counted.
measure :: (String, String) -&gt; Int
measure (_,ds) =
    length $ filter (=='c') ds'
    where ds' = dropWhile (=='c') [head a | a &lt;- group ds]

-- Tests if our word or stem ends with a given character
endswith :: (String, String) -&gt; Char -&gt; Bool
endswith (str,_) c = c == last str

-- Tests if our word or stem contains a vowel
hasvowel :: (String, String) -&gt; Bool
hasvowel (_,ds) = 'v' `elem` ds

-- Tests if our word or stem ends with a double consonant
endsdblc :: (String, String) -&gt; Bool
endsdblc (str,ds) =
    last ds == 'c' &amp;&amp; (last $ init str) == last str

-- Tests if our word or stem ends with the pattern 'cvc' and does
-- not end with the characters 'x', 'w' or 'y'
endscvc :: (String, String) -&gt; Bool
endscvc (str,ds) =
    drop (length ds - 3) ds == &quot;cvc&quot; &amp;&amp; last str `notElem` &quot;xwy&quot;
</code></pre>

<p>The first two functions here were described in more detail in the last post. To recap: The first rotates a string one character to the right. The second determines which letters in a word are consonants or vowels. How this is would likely be handled in an imperative language would be to keep track of the index of the letter we&rsquo;re currently evaluating so we could subtract 1 from that index and check the previous letter (which we need to do in the case of a &lsquo;y&rsquo;). That is, we need an index to know what character in a string we&rsquo;re evaluating. Haskell does have the operator !! which allows us to access a list by index but as I stated before I thought that was decidedly un-Haskelly. Instead, I opted to turn our string into a tuple where the first would be the word we&rsquo;re stemming and the second would be a string of either &ldquo;c&rdquo; or &ldquo;v&rdquo; to denote consonant or vowel.</p>

<p>The functions that follow show why I opted to do it this way: The algorithm sometimes wants to evaluate a word based on its constituent letters and sometimes wants to look at the pattern of consonants and vowels in that word. The functions endswith, hasvowel, and endsdblc (double consonant) don&rsquo;t need an explanation. The function measure is a bit more complex. What are we measuring? We&rsquo;re measuring the number of consonant-vowel sequences in a word.</p>

<pre><code class="language-makefile">A consonant will be denoted by c, a vowel by v. A list ccc... of length greater than 0 will be 
denoted by C, and a list vvv... of length greater than 0 will be denoted by V. Any word, 
or part of a word, therefore has one of the four forms:

    CVCV ... C
    CVCV ... V
    VCVC ... C
    VCVC ... V

These may all be represented by the single form

    [C]VCVC ... [V]

where the square brackets denote arbitrary presence of their contents.
Using (VC){m} to denote VC repeated m times, this may again be written as

    [C](VC){m}[V]
</code></pre>

<p>I&rsquo;ll admit that the &ldquo;denote arbitrary presence of their contents&rdquo; part confused me for quite a while. I had to read through the Python implementation and write my Haskell version before it made sense. It just means those bracketed letters (or letter sequences) may or may not be there. In retrospect I can&rsquo;t figure out why I couldn&rsquo;t figure it out &ndash; other than the fact that &ldquo;arbitrary&rdquo; isn&rsquo;t the correct word.</p>

<p>So, we need to scan through our word to the first vowel sequence and then count how many times we encounter a consonant sequence. The first part <code>[head a | a &lt;- group ds]</code> reduces our sequence (say &ldquo;ccvvccvvcc&rdquo;) to its transitions (&ldquo;cvcvc&rdquo;). We do that because for our purposes a &ldquo;cc&rdquo; is the same as &ldquo;c&rdquo;. The group function in Haskell groups like elements together in a list, so &ldquo;aab&rdquo; becomes [&ldquo;aa&rdquo;, &ldquo;b&rdquo;]. At the front of our list comprehension head takes the first of every group [&ldquo;a&rdquo;, &ldquo;b&rdquo;].</p>

<p>The function <code>dropWhile</code> will return a list starting from the first element that doesn&rsquo;t match the condition. In this case we&rsquo;ll drop the leading consonant, if any, so &ldquo;cvcv&rdquo; becomes &ldquo;vcv&rdquo;. The function filter operates in a similar way. It will return a list of elements that evaluate to true: &ldquo;vcv&rdquo; becomes &ldquo;c&rdquo;. From there we just count the number of elements in the list to get our measure. Another interesting Haskell operator here: $. The $ operator changes the precedence of the expression that follows. We could rewrite <code>length $ filter (=='c') ds'</code> as <code>length (filter (=='c') ds')</code>. Using $ is often cleaner looking.</p>

<p>It&rsquo;s illustrative to take a look at the Python version:</p>

<pre><code class="language-python"># Vivake Gupta (v@nano.com)
# http://tartarus.org/martin/PorterStemmer/python.txt

def m(self):
        n = 0
        i = self.k0
        while 1:
            if i &gt; self.j:
                return n
            if not self.cons(i):
                break
            i = i + 1
        i = i + 1
        while 1:
            while 1:
                if i &gt; self.j:
                    return n
                if self.cons(i):
                    break
                i = i + 1
            i = i + 1
            n = n + 1
            while 1:
                if i &gt; self.j:
                    return n
                if not self.cons(i):
                    break
                i = i + 1
            i = i + 1
</code></pre>

<pre><code class="language-haskell">measure :: (String, String) -&gt; Int
measure (_,ds) =
    length $ filter (=='c') ds'
    where ds' = dropWhile (=='c') [head a | a &lt;- group ds]
</code></pre>

<p>Which one is easier to read? To be fair this is a problem that Haskell is ideally suited to. (The Python version could be made more succinct as well. The version here was apparently translated line for line from the C version).</p>

<p>Ok, now on to the actual steps:</p>

<pre><code class="language-haskell">getstem :: String -&gt; String -&gt; String
getstem str sfx = take (length str - length sfx) str

swapsfx :: String -&gt; String -&gt; String -&gt; String
swapsfx str sfx [] = take (length str - length sfx) str
swapsfx str sfx sfx' = take (length str - length sfx) str ++ sfx'

step1a :: (String, String) -&gt; String
step1a (str,_) = swapsfx str (fst sfxs') (snd sfxs')
    where sfxs' = head $ dropWhile (\ss -&gt; not $ fst ss `isSuffixOf` str) sfxs
          sfxs = [
            (&quot;sses&quot;, &quot;ss&quot;),
            (&quot;ies&quot; , &quot;i&quot; ),
            (&quot;ss&quot;  , &quot;ss&quot;),
            (&quot;s&quot;   , &quot;&quot;  ) ]

step1b :: (String, String) -&gt; String
step1b (str,ds)
    | sfx &quot;eed&quot; &amp;&amp; (measure $ wordDesc $ stm &quot;eed&quot;) &gt; 0 = swp &quot;eed&quot; &quot;ee&quot;
    | sfx &quot;ed&quot; &amp;&amp; (hasvowel $ wordDesc $ stm &quot;ed&quot;)  = nf $ swp &quot;ed&quot; &quot;&quot;
    | sfx &quot;ing&quot; &amp;&amp; (hasvowel $ wordDesc $ stm &quot;ing&quot;)  = nf $ swp &quot;ing&quot; &quot;&quot;
    | otherwise = str
    where stm = getstem str
          sfx = (`isSuffixOf` str)
          swp = swapsfx str
          nf = step1b' . wordDesc

step1b' :: (String, String) -&gt; String
step1b' (str,ds)
    | or $ map sfx [&quot;at&quot;, &quot;bl&quot;, &quot;iz&quot;] = e
    | dbl &amp;&amp; (not . or $ map end &quot;lsz&quot;) = init str
    | measure (str,ds) == 1 &amp;&amp; endscvc (str,ds) = e
    | otherwise = str
    where sfx = (`isSuffixOf` str)
          dbl = endsdblc (str,ds)
          end = endswith (str,ds)
          e = str ++ &quot;e&quot;

step1c :: (String, String) -&gt; String
step1c (str,ds)
    | (hasvowel $ wordDesc $ stm &quot;y&quot;) &amp;&amp; end 'y' = init str ++ &quot;i&quot;
    | otherwise = str
    where stm = getstem str
          end = endswith (str,ds)
</code></pre>

<p>The two helper functions at the top of this section probably don&rsquo;t need an explanation. The first returns a word minus a given suffix. The second swaps a suffix with a new one. The first part of step 1 step1a begins the actual meat of our algorithm. It simply matches our word against the attached list of tuples in descending order. If our word ends with suffix on the left, we replace it with the suffix on the right.</p>

<p>Steps 2 - 5:</p>

<pre><code class="language-haskell">{-
    Step 2
    (m&gt;0) ATIONAL -&gt;  ATE
    ...
    (m&gt;0) BILITI  -&gt;  BLE
-}
step2 :: String -&gt; String
step2 str =
    if (not $ null sfxs') &amp;&amp; (measure $ snd $ wordDesc $ stm (fst sfx)) &gt; 0
    then swapsfx str (fst sfx) (snd sfx)
    else str
    where f = \ss -&gt; not $ fst ss `isSuffixOf` toLowers str
          stm = getstem str
          sfx = head $ sfxs'
          sfxs' = dropWhile f sfxs
          sfxs = [
            (&quot;ational&quot;, &quot;ate&quot; ),
            (&quot;tional&quot; , &quot;tion&quot;),
            (&quot;enci&quot;   , &quot;ence&quot;),
            (&quot;anci&quot;   , &quot;ance&quot;),
            (&quot;izer&quot;   , &quot;ize&quot; ),
            (&quot;abli&quot;   , &quot;able&quot;),
            (&quot;alli&quot;   , &quot;al&quot;  ),
            (&quot;entli&quot;  , &quot;ent&quot; ),
            (&quot;eli&quot;    , &quot;e&quot;   ),
            (&quot;ousli&quot;  , &quot;ous&quot; ),
            (&quot;ization&quot;, &quot;ize&quot; ),
            (&quot;ation&quot;  , &quot;ate&quot; ),
            (&quot;ator&quot;   , &quot;ate&quot; ),
            (&quot;alism&quot;  , &quot;al&quot;  ),
            (&quot;iveness&quot;, &quot;ive&quot; ),
            (&quot;fulness&quot;, &quot;ful&quot; ),
            (&quot;ousness&quot;, &quot;ous&quot; ),
            (&quot;aliti&quot;  , &quot;al&quot;  ),
            (&quot;iviti&quot;  , &quot;ive&quot; ),
            (&quot;biliti&quot; , &quot;ble&quot; )]

{-
    Step 3
    (m&gt;0) ICATE -&gt;  IC
    ...
    (m&gt;0) NESS  -&gt;
-}
step3 :: String -&gt; String
step3 str =
    if (not $ null sfxs') &amp;&amp; (measure $ snd $ wordDesc $ stm (fst sfx)) &gt; 0
    then swapsfx str (fst sfx) (snd sfx)
    else str
    where f = \ss -&gt; not $ fst ss `isSuffixOf` toLowers str
          stm = getstem str
          sfx = head $ sfxs'
          sfxs' = dropWhile f sfxs
          sfxs = [
            (&quot;icate&quot;, &quot;ic&quot;),
            (&quot;ative&quot;, &quot;&quot;  ),
            (&quot;alize&quot;, &quot;al&quot;),
            (&quot;iciti&quot;, &quot;ic&quot;),
            (&quot;ical&quot; , &quot;ic&quot;),
            (&quot;ful&quot;  , &quot;&quot;  ),
            (&quot;ness&quot; , &quot;&quot;  )]

{-
    Step 4
    (m&gt;1) AL -&gt;
    ...
    (m&gt;1 and (*S or *T)) ION -&gt;
    ...
    (m&gt;1) IZE  -&gt;
-}
step4 :: String -&gt; String
step4 str =
    if (not $ null sfxs') &amp;&amp; (measure $ snd $ wordDesc $ stm (fst sfx)) &gt; 1
    then swapsfx str (fst sfx) (snd sfx)
    else str
    where f = \ss -&gt; not $ fst ss `isSuffixOf` toLowers str
          stm = getstem str
          sfx = head $ sfxs'
          sfxs' = dropWhile f sfxs
          sfxs = [
            (&quot;al&quot;   , &quot;&quot; ),
            (&quot;ance&quot; , &quot;&quot; ),
            (&quot;ence&quot; , &quot;&quot; ),
            (&quot;er&quot;   , &quot;&quot; ),
            (&quot;ic&quot;   , &quot;&quot; ),
            (&quot;able&quot; , &quot;&quot; ),
            (&quot;ible&quot; , &quot;&quot; ),
            (&quot;ant&quot;  , &quot;&quot; ),
            (&quot;ement&quot;, &quot;&quot; ),
            (&quot;ment&quot; , &quot;&quot; ),
            (&quot;ent&quot;  , &quot;&quot; ),
            (&quot;sion&quot; , &quot;s&quot;),
            (&quot;tion&quot; , &quot;t&quot;),
            (&quot;ou&quot;   , &quot;&quot; ),
            (&quot;ism&quot;  , &quot;&quot; ),
            (&quot;ate&quot;  , &quot;&quot; ),
            (&quot;iti&quot;  , &quot;&quot; ),
            (&quot;ous&quot;  , &quot;&quot; ),
            (&quot;ive&quot;  , &quot;&quot; ),
            (&quot;ize&quot;  , &quot;&quot; )]

{-
    Step 5a
    (m&gt;1) E -&gt;
    (m=1 and not *o) E -&gt;
-}
step5a :: String -&gt; String
step5a str
    | sfx &quot;e&quot; &amp;&amp; (measure $ snd $ wordDesc $ stm &quot;e&quot;) &gt; 1 = swp &quot;e&quot; &quot;&quot;
    | sfx &quot;e&quot; &amp;&amp; (measure $ snd $ wordDesc $ stm &quot;e&quot;) == 1 &amp;&amp; noto = swp &quot;e&quot; &quot;&quot;
    | otherwise = str
    where noto = not $ endscvc $ wordDesc $ stm &quot;e&quot;
          stm = getstem str
          sfx = (`isSuffixOf` toLowers str)
          swp = swapsfx str

{-
    Step 5b
    (m &gt; 1 and *d and *L) -&gt;
-}
step5b :: String -&gt; String
step5b str =
    if (measure $ snd $ wordDesc str) &gt; 1 &amp;&amp; dblc &amp;&amp; ends 'l'
    then init str
    else str
    where dblc = endsdblc $ wordDesc str
          ends = endswith str

{-
    Our public stemming function
-}
stem :: String -&gt; String
stem str =
    steps str
    where steps = step5 . step4 . step3 . step2 . step1
          step1 = step1c . step1b . step1a
          step5 = step5b . step5a
</code></pre>

<p>As the comments suggest, each function here is the application of a series of rules, mostly with the form &ldquo;(measure&gt;n) SUFFIX -&gt; NEWSUFFIX&rdquo;. What that means in the context of the paper is if the measure of the word&rsquo;s stem (the word minus the first suffix) is over (or equal to) some number, then go ahead and swap the stems. Remember that a word&rsquo;s measure is the number of consonant-vowel sequences in the word. A more thorough explanation is here.</p>

<p>The general form in my implementation is to use dropWhile against a list of tuples (&ldquo;SUFFIX&rdquo;, &ldquo;NEWSUFFIX&rdquo;). The function dropWhile will return a list that begins with the first element that doesn&rsquo;t satisfy the given condition. In this case we drop all of the tuples where the first part of the tuple doesn&rsquo;t match our word. To check our suffix, Haskell has a function isSuffixOf that does exactly what you would expect.</p>

<p>The Python version of this algorithm is case-insensitive. I originally was going to make mine case sensitive (it was easier) and convert all input words to lowercase before processing them. As I was wrapping it up I decided I wanted the output to match the Python version so I could compare the two &ndash; so I went back through and made the whole thing case insensitive. Doing so was pretty straightforward. Haskell has a built in function toLower that will lowercase a character. I couldn&rsquo;t find a version that worked on strings so I added the toLowers that does just that.</p>

<p>The module&rsquo;s exported function stem composes the five main steps (and sub-steps) and returns our stemmed word. Pretty simple.</p>

<h3 id="general-observations-about-haskell">General Observations about Haskell</h3>

<p>With the algorithm done (but not thoroughly tested yet) I thought this would be a good point to review my initial impressions of working with Haskell. As of the date on this post I&rsquo;ve been working with the language for the equivalent of a few days (in small pieces, when I had the time), which decidedly isn&rsquo;t very much, but I have started to notice some patterns emerge:</p>

<ol>
<li>Most errors are caught as incompatible type issues.</li>
</ol>

<p>This is great. We can see one of the major benefits of the language&rsquo;s purity and strong, static typing here. For the most part, if you&rsquo;ve screwed up your syntax it will become immediately apparent because a function will be given the wrong number and/or wrong argument types.</p>

<ol>
<li>Most errors are syntax based.</li>
</ol>

<p>Following on #1, this might just be that I&rsquo;m really new to the language, but the majority of errors came from the fact that I had written an expression wrong. An common example would be to omit parentheses (or the $ or . operators). The upside here is those are generally easy to fix.</p>

<ol>
<li>Functions are black boxes that once written and tested, just work.</li>
</ol>

<p>While that&rsquo;s not completely true, or at least I wouldn&rsquo;t launch a rocket with that untested assumption, the purity of Haskell functions generally means that once you&rsquo;ve written a function that produces the expected output, you won&rsquo;t have to revisit that function later. The magic here comes from the lack of side effects and typing.</p>

<ol>
<li>If you&rsquo;re coming from an imperative-only background, there will be some head scratching, yes, but not that much.</li>
</ol>

<p>The utter alien-ness of the language is strong at first but dissipates pretty quickly. I would imagine this is especially true if you&rsquo;ve encountered list comprehensions and recursive functions in other languages. While those aren&rsquo;t the entirety of Haskell, they&rsquo;re the right mindset. My entirely unscientific evaluation is that functional languages do stuff &ldquo;in place&rdquo; while imperative languages do stuff &ldquo;in order&rdquo;. It helps me to think of a Haskell module as a single expression (on a single, very long line). While you wouldn&rsquo;t write it that way, I think it&rsquo;s a good mental model to have.</p>

<p>It&rsquo;s worth pointing out that not only are my impressions based on a very short period of time, they&rsquo;re also based on only a subset of the Haskell language. While this module does use the IO Monad, I skipped over working with Haskell&rsquo;s juicier bits like Monads, Haskell&rsquo;s data structures, the Maybe type, etc. I guess my next exercise will be to find a reason to use more of the language.</p>

        </div>

        
    </div>

    <div class="sidebar">
    <div class="container">
        <div class="sidebar-about">
            <img src="http://lattejed.com/img/profile_pic_met.png" />
            <a href="http://lattejed.com/"><h1>Matthew Smith</h1></a>
            <p class="lead">
            <p>Originally from the US, I've spent most of my post-college years as a resident of Bangkok, a city of award-winning chefs, beautiful people, traffic jams and the ocassional coup d'etat.</p>
            <p>I like tackling hard problems, most recently at the intersection of computer graphics, computational geomerty and product design while creating <a href="http://model3d.co">Model3D, a 3D modeler for iPad Pro and Apple Pencil</a>.</p>
            <p>Former co-founder of a PR agency, I found my true calling as a software engineer / entrepreneur. I've spent the last decade building products and consulting for iOS and macOS and have learned a dozen or so computer languages.</p> 
            <p>When I'm not working I spend time with my family. My wife Onne is GM of a company under the <a href="http://dwp.com">dwp</a> umbrella. My son Hadrian attends St. Andrews in central Bangkok.</p>
            </p>
        </div>

        

        <p>&copy; 2017. All rights reserved. </p>
    </div>
</div>


</body>
</html>
